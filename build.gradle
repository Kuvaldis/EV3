buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }
}

allprojects {
    group 'kuvaldis.ev3'
    apply plugin: 'idea'
}

//noinspection GroovyMissingReturnStatement
subprojects {

    ext {
        ev3Ssh = [
                user   : 'root', // todo: change to user
                keyfile: '${user.home}/.ssh/id_rsa'
        ]
    }

    configurations {
        ssh
    }

    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceSets {
        test {
            output.resourcesDir = output.classesDir
        }
    }

    jar {
        manifest {
            attributes("Class-Path": "/home/root/lejos/lib/ev3classes.jar /home/root/lejos/libjna/usr/share/java/jna.jar")
        }
    }

    shadowJar {
        dependencies {
            exclude(dependency(':ev3classes:'))
        }
    }

    build.dependsOn shadowJar

    repositories {
        flatDir {
            dirs "$rootDir/lib"
        }
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    dependencies {
        compile name: 'ev3classes'

        testCompile group: 'junit', name: 'junit', version: '4.12'

        ssh 'org.apache.ant:ant-jsch:1.9.6'
        ssh 'com.jcraft:jsch:0.1.53'
    }

    task deploy << {
        group = 'deploy'
        description = 'Deploys jar to EV3'

        final jarToDeploy = shadowJar.outputs.files.findResult { it.name.contains('.jar') ? it : null }
        final jarName = jarToDeploy.name
        println "Deploy $jarName"

        ant.taskdef(name: 'scp', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
                classpath: configurations.ssh.asPath)
        ant.taskdef(name: 'sshExec', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.SSHExec',
                classpath: configurations.ssh.asPath)

        ant.scp(todir: "$ev3Ssh.user@10.0.1.1:/home/lejos/programs",
                keyfile: ev3Ssh.keyfile,
                verbose: true,
                file: jarToDeploy)
        ant.sshExec(host: '10.0.1.1',
                username: ev3Ssh.user,
                keyfile: ev3Ssh.keyfile,
                verbose: true,
                command: "jrun -jar /home/lejos/programs/$jarName")
    }
    deploy.dependsOn build
}

